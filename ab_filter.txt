FUNCTION_BLOCK ABFilter
TITLE = 'ABFilter'
NAME : 'AB'
VERSION : '1.0'
//KNOW_HOW_PROTECT
AUTHOR : 'DDS'
VAR_INPUT
    Td: REAL;//Period diskret
    newVal: REAL;//PV input
    sigma_process: REAL;//3.0 default
    sigma_noise: REAL;//0.7 default
END_VAR
VAR_OUTPUT
    out: REAL;//Filtered value
END_VAR
VAR_TEMP
    lambda: REAL;
    r: REAL;
END_VAR
VAR
    xk_1: REAL := 0.0;
    vk_1: REAL := 0.0;
    a: REAL := 0.0;
    b: REAL := 0.0;
    xk: REAL := 0.0;
    vk: REAL := 0.0;
    rk: REAL := 0.0;
    xm: REAL := 0.0;
END_VAR
    lambda := sigma_process * Td * Td / sigma_noise;
    r := (4.0 + lambda - SQRT(8.0 * lambda + lambda * lambda)) / 4.0;
    a := 1.0 - r * r;
    b := 2.0 * (2.0 - a) - 4.0 * SQRT(1.0 - a);
    xm := newVal;
    xk := xk_1 + (vk_1 * Td);
    vk := vk_1;
    rk := xm - xk;
    xk := xk + a * rk;
    vk := vk + ( b * rk ) / Td;
    xk_1 := xk;
    vk_1 := vk;
    out := xk_1;
END_FUNCTION_BLOCK